plugins {
  id 'java'
  id 'org.springframework.boot' version '3.4.12'
  id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.kokk'
version = '0.0.1-SNAPSHOT'
description = 'concert'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}
bootJar {
  enabled = true
  archiveFileName = "myapp.jar"
}

jar {
  enabled = false   // 보통은 plain jar 필요 없으면 끔
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  compileOnly 'org.projectlombok:lombok'
  runtimeOnly 'com.mysql:mysql-connector-j'
  annotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  // Security
  implementation 'org.springframework.boot:spring-boot-starter-security'

  // JWT (JJWT 0.11.x)
  implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
  runtimeOnly   'io.jsonwebtoken:jjwt-impl:0.11.5'
  runtimeOnly   'io.jsonwebtoken:jjwt-jackson:0.11.5'

  // Swagger
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

  // Redis
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'

  // Redisson
  implementation 'org.redisson:redisson-spring-boot-starter:3.27.0'

  // Validation
  implementation "org.springframework.boot:spring-boot-starter-validation"

  //Kafka
  implementation 'org.springframework.kafka:spring-kafka'

  // QueryDsl
  implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
  annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
  implementation "com.querydsl:querydsl-jpa:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
  implementation "com.querydsl:querydsl-core"
  annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"

  // JPA Annotation Processor (QClass 생성 시 필요)
  annotationProcessor "jakarta.annotation:jakarta.annotation-api" // java.lang.NoClassDefFoundError
  annotationProcessor "jakarta.persistence:jakarta.persistence-api" // java.lang.NoClassDefFoundError

}

// Querydsl 빌드 옵션 설정
def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
  options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
  main.java.srcDirs += [ generated ]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
  delete file(generated)
}

tasks.named('test') {
  useJUnitPlatform()
}
